---
title: "The painful experience of automating a consumption based Logic App that accesses a Key Vault with a managed identity via Terraform"
date: 2024-08-27 00:00:00 +0200
categories: 
    - Terraform
post-id: 0009
---

## Intro

I needed to create an Azure Logic App that could access a Key Vault via a managed identity, and i wanted it to be 100% managed via Terraform. It needed to be a workflow/consumption based model.

There is not a lot of information on how to do what I wanted to do. So I figured I would post some of the stuff I learned.

## My struggles
### Deploying API Connection connecting to Key Vault via azurerm_api_connection
I started out using the built-in native "Key Vault" actions (spoiler: don't). They require what is called an API Connection to be created, this API Connection is another resource in Azure. The API Connection then points to the correct Key Vault API endpoint and it contains the Managed Identity information for the Logic App. So the API Connection is used to define what Key Vault to connect to, and how to authenticate.

In Terraform with the provider azurerm, there is a resource type called "azurerm_api_connection" which you can use to provision an API Connection. The problem in this case is that you have to supply a "vaultName" via the alternativeParameterValues parameter, however that is not possible in azurerm_api_connection. So i ended up with the API Connection almost configured, i just needed to supply the vaultName, but i was unable to via Terraform.

So i ended up deploying the API Connection via an ARM template by using azurerm_resource_group_template_deployment. However due to another problem, i actually ended up removing the API Connection and not even needing it anyway.

### Using the "Key Vault" actions
In the end, it didn't even matter, because when i finally got the API Connection working via an ARM template, it was unusable inside the Logic App. The "Azure Key Vault" actions showed an error stating i was using the wrong Key Vault API version. I didn't find a way of changing it, i think the api-version is hardcoded into the actions. 

So i migrated to using the HTTP action, which didn't even require the API Connection. 

![](assets/post-content/0009/Screenshot 2024-08-25 081632.png)

### Freaking case sensitive
Just as i thought i was done, because i had created all actions except from the last one, i ran into another problem. For whatever reason, when i added the last action "Update apikey secret" it just broke everything. When viewing the Logic App in designer it was just completly blank. After 2 hours of troubleshooting i figured out it was because the runAfter is case sensitive.


```json
"runAfter": {
    "Extract_apikey_from_Json": [
        "Succeeded"
    ]
}
 
"runAfter": {
    "Extract_apikey_from_json": [
        "Succeeded"
    ]
}
```

## My experience
Using Terraform to deploy the Logic App took a long time, and i ran into way more problems than i had expected. Maybe i was just unlucky with the Key Vault actions, i don't know, this is the first time i'm automating it, maybe i will update this post in the future, if i automate another Logic App using different actions. The HTTP actions i didn't have a hard time automating.

A tip is creating the Logic App in the portal, and then using the code from the "Logic app code view" inside Terraform.

I hear that the standard Logic App is easier to automate than the consumption based Logic App, but it is not something i have tried.

Before Terraform
![](assets/post-content/0009/Screenshot 2024-08-25 085454.png)

After Terraform
![](assets/post-content/0009/Screenshot 2024-08-25 083430.png)

## The final code
Unfortunately i was not using version-control, so i don't have pieces of the old code, i only have the final result.

```terraform
##### Consumption based Logic App #####
### Logic App environment setup ###
resource "azurerm_logic_app_workflow" "apikey" {
  name = "${var.prefix}-apikey"
  resource_group_name = var.rg-name
  location = var.region

  identity {
    type = "SystemAssigned"
  }
}

### Configuring triggers and actions on the Logic App ###
## Triggers ##
resource "azurerm_logic_app_trigger_recurrence" "apikey" {
  name = "Scheduled trigger"
  logic_app_id = azurerm_logic_app_workflow.apikey.id
  frequency = "Hour"
  interval = "2"
  start_time = "2024-08-23T15:24:48Z"
}

## Actions ##
# Get Username from vault
resource "azurerm_logic_app_action_custom" "getusernamefromvault" {
  name = "Get_username_from_vault"
  logic_app_id = azurerm_logic_app_workflow.apikey.id
  body = <<BODY
{
    "inputs": {
        "authentication": {
            "audience": "https://vault.azure.net",
            "type": "ManagedServiceIdentity"
        },
        "method": "GET",
        "uri": "https://${azurerm_key_vault.apikey.name}.vault.azure.net/secrets/username?api-version=7.2"
    },
    "runAfter": {},
    "runtimeConfiguration": {
        "contentTransfer": {
            "transferMode": "Chunked"
        },
        "secureData": {
            "properties": [
                "inputs",
                "outputs"
            ]
        }
    },
    "type": "Http"
}
  BODY
}

# Extract username from request
resource "azurerm_logic_app_action_custom" "extractusernamefromrequest" {
  name = "Extract_username_from_request"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.getusernamefromvault ]

  body = <<BODY
{
    "inputs": {
        "content": "@body('Get_username_from_vault')",
        "schema": {
            "properties": {
                "value": {
                    "type": "string"
                }
            },
            "type": "object"
        }
    },
    "runAfter": {
        "Get_username_from_vault": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "secureData": {
            "properties": [
                "inputs"
            ]
        }
    },
    "type": "ParseJson"
}
  BODY
}

# Get password from vault
resource "azurerm_logic_app_action_custom" "getpasswordfromvault" {
  name = "Get_password_from_vault"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.extractusernamefromrequest ]

  body = <<BODY
{
    "inputs": {
        "authentication": {
            "audience": "https://vault.azure.net",
            "type": "ManagedServiceIdentity"
        },
        "method": "GET",
        "uri": "https://${azurerm_key_vault.apikey.name}.vault.azure.net/secrets/password?api-version=7.2"
    },
    "runAfter": {
        "Extract_username_from_request": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "contentTransfer": {
            "transferMode": "Chunked"
        },
        "secureData": {
            "properties": [
                "inputs",
                "outputs"
            ]
        }
    },
    "type": "Http"
}
  BODY
}

# Extract password from request
resource "azurerm_logic_app_action_custom" "extractpasswordfromrequest" {
  name = "Extract_password_from_request"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.getpasswordfromvault ]

  body = <<BODY
{
    "inputs": {
        "content": "@body('Get_password_from_vault')",
        "schema": {
            "properties": {
                "value": {
                    "type": "string"
                }
            },
            "type": "object"
        }
    },
    "runAfter": {
        "Get_password_from_vault": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "secureData": {
            "properties": [
                "inputs"
            ]
        }
    },
    "type": "ParseJson"
}
  BODY
}

# Acquire apikey
resource "azurerm_logic_app_action_custom" "acquireapikey" {
  name = "Acquire_apikey"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.extractpasswordfromrequest ]

  body = <<BODY
{
    "inputs": {
        "body": {
            "password": "@{body('Extract_password_from_request')?['value']}",
            "username": "@{body('Extract_username_from_request')?['value']}"
        },
        "headers": {
            "Content-Type": "application/json"
        },
        "method": "POST",
        "uri": "${var.apikey-endpointforlogin}"
    },
    "runAfter": {
        "Extract_password_from_request": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "contentTransfer": {
            "transferMode": "Chunked"
        },
        "secureData": {
            "properties": [
                "inputs",
                "outputs"
            ]
        },
        "staticResult": {
            "name": "HTTP0",
            "staticResultOptions": "Disabled"
        }
    },
    "type": "Http"
}
  BODY
}

# Extract apikey from json
resource "azurerm_logic_app_action_custom" "extractapikeyfromjson" {
  name = "Extract_apikey_from_json"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.acquireapikey ]

  body = <<BODY
{
    "inputs": {
        "content": "@body('Acquire_apikey')",
        "schema": {
            "properties": {
                "apiKey": {
                    "type": "string"
                }
            },
            "type": "object"
        }
    },
    "runAfter": {
        "Acquire_apikey": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "secureData": {
            "properties": [
                "inputs"
            ]
        }
    },
    "type": "ParseJson"
}
  BODY
}

# Update apikey secret
resource "azurerm_logic_app_action_custom" "updateapikeysecret" {
  name = "Update_apikey_secret"
  logic_app_id = azurerm_logic_app_workflow.apikey.id

  depends_on = [ azurerm_logic_app_action_custom.extractapikeyfromjson ]

  body = <<BODY
{
    "inputs": {
        "authentication": {
            "audience": "https://vault.azure.net",
            "type": "ManagedServiceIdentity"
        },
        "body": {
            "value": "@{body('Extract_apikey_from_JSON')?['apiKey']}"
        },
        "method": "PUT",
        "uri": "https://${azurerm_key_vault.apikey.name}.vault.azure.net/secrets/apikey?api-version=7.2"
    },
    "runAfter": {
        "Extract_apikey_from_json": [
            "Succeeded"
        ]
    },
    "runtimeConfiguration": {
        "contentTransfer": {
            "transferMode": "Chunked"
        },
        "secureData": {
            "properties": [
                "inputs",
                "outputs"
            ]
        }
    },
    "type": "Http"
}
  BODY
}
```

## Helpful links
[https://learn.microsoft.com/en-us/azure/logic-apps/authenticate-with-managed-identity?tabs=consumption#arm-template-for-api-connections-and-managed-identities](https://learn.microsoft.com/en-us/azure/logic-apps/authenticate-with-managed-identity?tabs=consumption#arm-template-for-api-connections-and-managed-identities)

[https://aztoso.com/logic-app/keyvault-connector-with-managed-identity/](https://aztoso.com/logic-app/keyvault-connector-with-managed-identity/)

[https://learn.microsoft.com/en-us/rest/api/keyvault/secrets/get-secret/get-secret?view=rest-keyvault-secrets-7.4&tabs=HTTP](https://learn.microsoft.com/en-us/rest/api/keyvault/secrets/get-secret/get-secret?view=rest-keyvault-secrets-7.4&tabs=HTTP)

[https://alessandromoura.azurewebsites.net/2022/04/28/creating-managed-identities-api-connections-for-logic-apps-in-bicep-arm/](https://alessandromoura.azurewebsites.net/2022/04/28/creating-managed-identities-api-connections-for-logic-apps-in-bicep-arm/)